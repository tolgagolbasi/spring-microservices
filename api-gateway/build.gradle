/*
* Build file for API-Gateway
*/
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'eclipse'

buildscript {
	project.ext {
         springBootVersion = '2.4.1'
         jarName = 'basic-api-gateway'
         versionName = '0.0.1'
         gradleDockerVersion = '1.2'
         swagger2version = '2.7.0'
	}
    repositories {
        jcenter()
        mavenCentral()
    }
    
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "se.transmode.gradle:gradle-docker:${project.gradleDockerVersion}"
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '6.7.1'
}

/**
* These values(group & mainClassName) are required by the gradle docker plugin.
*
* The "group" value feeds into the docker tag and is required if you want to push the images
* to docker hub.
*
* The "mainClassName" value tells which class has the "main" entry point for running the 
* Spring boot application.
*/

group = 'anilallewar'
mainClassName = 'com.anilallewar.microservices.gateway.GatewayApplication'
sourceCompatibility = 15
targetCompatibility = 15

repositories {
    mavenCentral()
    jcenter()
}



dependencies {
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:2020.0.0')

    // Basic Spring boot with config client
    implementation('org.springframework.cloud:spring-cloud-starter-config:2.2.6.RELEASE')
    implementation 'org.springframework.boot:spring-boot-starter-web:2.4.1'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.4.1'
    implementation("org.springframework.cloud:spring-cloud-starter-netflix-zuul:2.2.6.RELEASE")
    
    // Spring OAuth2 security
    implementation("org.springframework.boot:spring-boot-starter-security:2.4.1")
    implementation("org.springframework.cloud:spring-cloud-starter-oauth2:2.2.4.RELEASE")
	implementation("org.springframework.security:spring-security-jwt:1.1.1.RELEASE")
    implementation 'com.google.code.gson:gson:2.8.6'

    // Eureka client
    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:2.2.6.RELEASE')
	
	// Zipkin tracing
	implementation('org.springframework.cloud:spring-cloud-starter-zipkin:2.2.6.RELEASE')
	
	  	// Swagger for API testing
	implementation("io.springfox:springfox-swagger2:3.0.0")
	implementation("io.springfox:springfox-swagger-ui:3.0.0")
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth:2.2.6.RELEASE'

}

archivesBaseName = "${project.jarName}"
version = "${project.versionName}"

/*
* This task builds the docker image by copying the output of the "jar" gradle command
* and moving it to the "build/docker" directory which is used as the staging directory 
* by the docker gradle plugin. We then build the docker image by invoking the appropriate
* Dockerfile.  
*/
task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = jar.baseName
  tagVersion = jar.version
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
  	// Copy the built fat jar to the Docker plugin staging directory
    copy {
      from jar
      into stageDir
    }
    // Copy the run.sh file to the Docker plugin staging directory
    copy {
      from "${project.buildDir}/resources/main/run.sh"
      into stageDir
    }
  }
}

run {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=4300,suspend=n','-Dspring.profiles.active=default']
}