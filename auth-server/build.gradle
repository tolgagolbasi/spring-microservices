/*
* Build file for authorization server
*/
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'eclipse'

buildscript {
	project.ext {
         springBootVersion = '2.4.1'
         jarName = 'basic-auth-server'
         versionName = '0.0.1'
         gradleDockerVersion = '1.2'
         swagger2version = '2.7.0'
	}
    repositories {
        jcenter()
        mavenCentral()
    }
    
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        classpath "se.transmode.gradle:gradle-docker:${project.gradleDockerVersion}"
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '6.7.1'
}

// Used by the Docker gradle plugin, group refers to the account under which the docker image is created
group = 'anilallewar'
mainClassName = 'com.anilallewar.microservices.auth.AuthServerApplication'
sourceCompatibility = 15
targetCompatibility = 15

repositories {
    mavenCentral()
    jcenter()
}



archivesBaseName = "${project.jarName}"
version = "${project.versionName}"

dependencies {
    implementation platform('org.springframework.cloud:spring-cloud-dependencies:2020.0.0')
    // Basic Spring boot with config client
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.4.1'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.4.1'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker:2.4.1'
    implementation 'com.google.code.gson:gson:2.8.6'

    // Spring OAuth2 security
    implementation("org.springframework.boot:spring-boot-starter-security:2.4.1")
    implementation("org.springframework.security:spring-security-jwt:1.1.1.RELEASE")
    // https://mvnrepository.com/artifact/org.springframework.security.oauth/spring-security-oauth2
    implementation('org.springframework.security.oauth:spring-security-oauth2:2.4.1.RELEASE')

    // Eureka client
    implementation('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:2.2.6.RELEASE')

    // JPA for persisting user data
	implementation("org.springframework.boot:spring-boot-starter-data-jpa:2.4.1")
	runtimeOnly("mysql:mysql-connector-java:8.0.22")
	
	// Swagger for API testing
	implementation("io.springfox:springfox-swagger2:3.0.0")
	implementation("io.springfox:springfox-swagger-ui:3.0.0")

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth:2.2.6.RELEASE'
    // Zipkin tracing
    implementation('org.springframework.cloud:spring-cloud-starter-zipkin:2.2.6.RELEASE')

}



/*
* This task builds the docker image by copying the output of the "jar" gradle command
* and moving it to the "build/docker" directory which is used as the staging directory 
* by the docker gradle plugin. We then build the docker image by invoking the appropriate
* Dockerfile.  
*/
task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = jar.baseName
  tagVersion = jar.version
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    // Copy the built fat jar to the Docker plugin staging directory
    copy {
      from jar
      into stageDir
    }
    // Copy the run.sh file to the Docker plugin staging directory
    copy {
      from "${project.buildDir}/resources/main/run.sh"
      into stageDir
    }
  }
}

run {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=4100,suspend=n','-Dspring.profiles.active=default']
}
